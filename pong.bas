@START
' TODO: Improvements
'  - delay before new match
'  - ball hitting change its movement
'    
BGMPLAY 0,0,127
ACLS
COLOR 0
PRINT "┌─────────────────────────────┐"
PRINT "│PONG                         │"
PRINT "│ First Osvaldo's Game        │"
PRINT "└─────────────────────────────┘"
COLOR 5
LOCATE 10,21
PRINT "Press START"

S=0
@K
B = (BUTTON(2) AND 1024) == 1024
'IF B THEN S=S+1 ELSE S=0
IF B THEN GOSUB @GAME:GOTO @START
VSYNC 1
GOTO @K

@GAME

ACLS:PAL=3

' BALL
BN=0
BW=16
BH=16
BX=128:BY=96
IBX=2:IBY=1
SPSET  BN, 143, PAL, 0, 0, 2, BW, BH
SPCOL  BN, -BW/2, -BH/2, BW, BH, FALSE
SPHOME BN, BW/2, BH/2

L=0
LN=1
LW=16
LH=32
LX=8
LY=96
SPSET  LN, 312, PAL, 1, 0, 2, LW, LH
SPCOL  LN, -LW/2, -LH/2, LW, LH, FALSE
SPHOME LN, LW/2, LH/2

R = 0
RN=2
RW=16
RH=32
RX=246
RY=96
SPSET  RN, 312, PAL, 0, 0, 2, RW, RH
SPCOL  RN, -RW/2, -RH/2, RW, RH, FALSE
SPHOME RN, RW/2, RH/2

GOSUB @SCORE

Q=0

@MAINLOOP
B = BUTTON()
IF (B AND 1) == 1 AND LY > 1 THEN LY = LY - 1
IF (B AND 2) == 2 AND LY < 175 THEN LY = LY + 1
IF (B AND 64) == 64 AND RY > 1 THEN RY = RY - 1
IF (B AND 32) == 32 AND RY < 175 THEN RY = RY + 1

S=((B AND 1024) == 1024)
IF S THEN Q=Q+1 ELSE Q=0
IF Q > 20 THEN RETURN


HIT=SPHIT(BN)
IF HIT == 1 AND BX < 128 THEN BEEP 12:IBX=2
IF HIT == 1 AND BX > 128 THEN BEEP 12:IBX=-2
IF BX<8 THEN GOSUB @RWIN
IF BX>246 THEN GOSUB @LWIN
BX=BX+IBX

IF BY<16 OR BY>174 THEN IBY=-IBY
BY=BY+IBY


' DRAW
SPOFS BN, BX, BY
SPOFS LN, LX, LY
SPOFS RN, RX, RY
VSYNC 1

' MAIN LOOP
GOTO @MAINLOOP


@RWIN
BX=128
BY=96
IBX=2

R=R+1
GOSUB @SCORE
RETURN


@LWIN
BX=128
BY=96
IBX=-2

L=L+1
GOSUB @SCORE
RETURN

@SCORE
R$=STR$(R)
L$=STR$(L)

LOCATE (16-LEN(L$))/2,0
PRINT L

LOCATE (16-LEN(R$))/2+16,0
PRINT R

RETURN


